apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// See https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ex) {
    logger.warn("No keys.  Release disabled: $ex.message")
    keystoreProperties['keyAlias'] = ''
    keystoreProperties['keyPassword'] = ''
    keystoreProperties['storeFile'] = '/dev/null'
    keystoreProperties['storePassword'] = ''
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "app.intra"
        // The Java code is compatible back to SDK version 14, but gomobile requires
        // a minSdkVersion of 15.
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 47
        versionName "1.2.4"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        // Ignore lint errors that we believe are safe to ignore.
        baseline file("lint-baseline.xml")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.config
            resValue("bool", "FIREBASE_ENABLED", "true")
        }
        debug {
            pseudoLocalesEnabled true
            resValue("bool", "FIREBASE_ENABLED", "false")
            // Split build across multiple dex files.  This is necessary mostly because Guava adds
            // so many classes that we exceed the dex limit (65536 methods).  This does not apply
            // to release builds, where unused classes are pruned.
            multiDexEnabled true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

crashlytics {
    enableNdk true
}

repositories {
    maven {
        url 'test-repo'
    }
    maven {
        url '../gradle-plugin-repo'
    }
    maven {
        url '../firebase-repo'
    }
}

dependencies {
    // Third-party dependency: OkHttp3
    implementation fileTree(include: [
            'okhttp-3.12.3.jar',  // 3.12.* has branched to support API <21.  3.13+ doesn't.
            'okio-1.15.0.jar',
            'guava-27.1-android.jar'
    ], dir: 'libs')

    // Third-party test dependencies.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20171018'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    // UI libraries.
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // For Firebase Analytics, etc.
    implementation 'com.google.firebase:firebase-core:16.0.9'  // Last version to support API <16
    implementation 'com.google.firebase:firebase-perf:16.2.5'  // Last version to support API <17
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.0'
    implementation 'com.google.firebase:firebase-config:16.5.0' // Last version to support API <16
    // For go-tun2socks
    implementation project(":tun2socks")
}
// For Firebase Analytics
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
