apply plugin: 'com.android.application'

// See https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "app.intra"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 6
        versionName "0.0.5"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}
repositories {
    maven {
        url 'test-repo'
    }
}

dependencies {
    // Third-party dependency: OkHttp3
    implementation fileTree(include: [
            'okhttp-3.11.0.jar',
            'okio-1.15.0.jar',
            'guava-26.0-android.jar'
    ], dir: 'libs')

    // Third-party test dependencies.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20171018'
    testImplementation 'org.mockito:mockito-core:2.13.0'

    // UI libraries.
    implementation 'androidx.appcompat:appcompat:1.0.0-rc01'
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation 'androidx.preference:preference:1.0.0-rc01'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0-rc01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    // For Firebase Analytics, etc.
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-perf:16.1.0'
    implementation 'com.google.firebase:firebase-crash:16.0.1'
}
// For Firebase Analytics
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
